SIMULATE					; pocetak simulacije

GENERATE X$SVD,FN$EXPO				; 1 dolazak osobe
QUEUE SALTERRED					; 2 osoba staje u red za salter
ENTER SALTER					; 3 osoba zauzima salter
DEPART SALTERRED				; 4 osoba napusta red za salter
ADVANCE 180,60					; 5 prolazi 3+-1 minuta
LEAVE SALTER					; 6 osoba oslobadja salter
TABULATE HIST					; 7 beleze se podaci za histogram
TRANSFER .4,FILM,KOKICE				; 8 60% ide na film, ostali da kupe kokice

KOKICE TEST LE X$BRREDKOKICE,4,ODUST		; 9 ako je red duzi od 4, skok na ODUST
QUEUE KOKICERED					; 10 osoba staje u red za kokice
SAVEVALUE BRREDKOKICE+,1			; 11 povecava se broj osoba u redu za kokice za 1
SEIZE APARAT					; 12 osoba zauzima aparat za kokice
DEPART KOKICERED				; 13 osoba napusta red za kokice
ASSIGN 1,V$BRKUT				; 14 osoba se odlucuje koliko kutija kokica uzima ( max 5)
TEST GE X$PREOSTALEKUT,V$BRKUT,NEMAKUT		; 15 ako u aparatu nema dovoljno kutija, skok na NEMAKUT
ADVANCE V$VRKUP					; 16 prolazi n*30+10 sekundi ( n broj kutija)
RELEASE APARAT					; 17 osoba oslobadja aparat za kokice
SAVEVALUE PREOSTALEKUT-,V$BRKUT			; 18 smanjuje se broj preostalih kutija u aparatu
SAVEVALUE BRREDKOKICE-,1			; 19 smanjuje se broj osoba u redu za kokice za 1
TRANSFER ,FILM					; 20 osoba odlazi na film

ODUST SAVEVALUE BRODUST+,1			; 21 povecava se broj odustalih
TRANSFER ,FILM					; 22 osoba odlazi na film

NEMAKUT SAVEVALUE BRREDKOKICE-,1 		; 23 smanjuje se broj osoba u redu za kokice
RELEASE APARAT					; 24 osoba oslobadja aparat za kokice
TRANSFER ,FILM					; 25 osoba odlazi na film

FILM TERMINATE					; 26 kraj procesa koji prati osobu

GENERATE 10800					; 27 radnik dolazi na svaka 3 sata 
ADVANCE 600,120					; 28 prolazi 10+-2 minuta
SAVEVALUE PREOSTALEKUT,40			; 29 radnik puni aparat za kokice kutijama
TERMINATE					; 30 kraj procesa koji prati radnika

GENERATE 3600					; 31
TERMINATE 1					; 32

HIST TABLE M1,100,100,6				; initijalizacija histograma
EXPO FUNCTION RN1,C24				; EXPO funkcija
0,0/.1,.104/.2,.222/.3,.355/.4,.509/.5,.69/.6,.915/.7,1.2/.75,1.38/.8,1.6/.84,1.83/.88,2.12/.9,2.3/.92,2.52/.94,2.81/.95,2.99/.96,3.2/.97,3.5/.98,3.9/.99,4.6/.995,5.3/.998,6.2/.999,7/.9998,8
INITIAL X$SVD,90				; srednja vrednost vremena dolaska osobe
BRKUT VARIABLE X$MAXBRKUT*RN1/1000+1		; broj kutija kokica za koji se osoba odlucuje
VRKUP VARIABLE P1*30+100			; vreme kupovine kokica u funkciji od broja kutija
INITIAL X$MAXBRKUT,5				; najveci broj kutija kokica koji jedna osoba moze da uzme
INITIAL X$BRREDKOKICE,0				; broj osoba u redu za kokice
INITIAL X$BRODUST,0				; broj odustalih od kokica zbog duzine reda
INITIAL X$PREOSTALEKUT,40			; broj preostalih kutija u aparatu za kokice
SALTER STORAGE 3				; broj saltera za prodaju karata

START 8						; razmatramo period od 8 casova

END						; kraj simulacije